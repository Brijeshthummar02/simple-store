apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: "com.diffplug.gradle.spotless"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat()
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":simplestore")
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    annotationProcessor "com.uber.nullaway:nullaway:0.6.4"
    testAnnotationProcessor "com.uber.nullaway:nullaway:0.6.4"

    testImplementation "com.google.truth:truth:0.42"
    testImplementation "org.robolectric:robolectric:4.1"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')